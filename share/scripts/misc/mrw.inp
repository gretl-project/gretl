# Replicate Table 1, "Estimation of the Textbook
# Solow model," in Mankiw, Romer and Weil, QJE 1992
open mrw.gdt
series lny = log(gdp85)
series ngd = 0.05 + (popgrow/100.0)
series lngd = log(ngd)
series linv = log(invest/100.0)
# generate variable for testing Solow restriction
series x3 = linv - lngd
# set sample to non-oil producing countries
smpl nonoil --dummy
model1 <- ols lny const linv lngd
scalar essu = $ess
scalar dfu1 = $df
# restricted regression
ols lny const x3
scalar F1 = ($ess - essu)/(essu/dfu1)
# set sample to the "better data" countries
smpl intermed --dummy --replace
model2 <- ols lny const linv lngd
scalar essu = $ess
scalar dfu2 = $df
# restricted regression
ols lny const x3
scalar F2 = ($ess - essu)/(essu/dfu2)
# set sample to the OECD countries
smpl OECD --dummy --replace
model3 <- ols lny const linv lngd
scalar essu = $ess
scalar dfu3 = $df
# restricted regression
ols lny const x3
scalar F3 = ($ess - essu)/(essu/dfu3)
print F1 F2 F3
# pvalues for test of Solow restriction in each sample
pvalue F 1 dfu1 F1
pvalue F 1 dfu2 F2
pvalue F 1 dfu3 F3


