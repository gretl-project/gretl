dnl
dnl Process this file with autoconf to produce a configure script.
dnl

AC_INIT(lib/src/version.h)
AC_PREREQ(2.12)
AC_CONFIG_HEADER(config.h)
AC_CONFIG_AUX_DIR($srcdir/tools)

PACKAGE=gretl

VERSION=`grep GRETL_VERSION $srcdir/lib/src/version.h | awk '{ print $NF }' | sed -e 's/\"//g'`
LIBGRETL_CURRENT=`grep LIBGRETL_CURRENT $srcdir/lib/src/version.h | awk '{ print $NF }'`
LIBGRETL_REVISION=`grep LIBGRETL_REVISION $srcdir/lib/src/version.h | awk '{ print $NF }'`
LIBGRETL_AGE=`grep LIBGRETL_AGE $srcdir/lib/src/version.h | awk '{ print $NF }'`
LIBVERSION="$LIBGRETL_CURRENT.$LIBGRETL_AGE.$LIBGRETL_REVISION"

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(LIBVERSION, "$LIBVERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(LIBVERSION)

AC_SUBST(LIBGRETL_CURRENT)
AC_SUBST(LIBGRETL_REVISION)
AC_SUBST(LIBGRETL_AGE)

echo "configuring for $PACKAGE $VERSION (library version $LIBVERSION)"

dnl
dnl Variables controlling the gretl build
dnl
build_gui="yes"
build_docs="no"
use_lucida="no"
build_po="no"
have_pkgconfig="no"
have_readline="no"
new_readline="no"
have_zlib="no"
have_gtk="no"
have_gtkprint="no"
have_gnome="no"
have_gmp="no"
have_mpfr="no"
have_lapack="no"
have_fftw3="no"
seek_inet_addr="no"
have_tramo="yes"
have_x12a="yes"
have_audio="no"
have_ox="no"
try_sourceview="yes"
have_sourceview="no"
bundled_sourceview="yes"
have_gnu_regex="no"
have_libxml2="no"
have_xslt="no"
have_odbc="no"
try_libR="yes"
have_libR="no"
check_gnuplot="yes"
darwin_build="no"
quiet_build="no"
MAKE="make"
XML_LIBS=
FONTREQ="\\RequirePackage{ae}"

AC_CHECK_HEADERS(libintl.h,
	try_nls="yes",
	try_nls="no"
)

if test [ "$try_nls" = "yes" ]
then
   AM_GNU_GETTEXT([external],,)
fi

dnl
dnl Process options supplied to the configure script
dnl
AC_ARG_ENABLE(gui,
[  --enable-gui		      Enable gui [[default=yes]]],
[if test "$enableval" = "yes"
then
  build_gui=yes
else
  build_gui=no
fi])

AC_ARG_ENABLE(build-doc,
[  --enable-build-doc      Enable building of gretl docs [[CVS only]]],
[if test "$enableval" = "yes"
then
  build_docs=yes
else
  build_docs=no
fi])

AC_ARG_ENABLE(quiet-build,
[  --enable-quiet-build     Enable quiet building],
[if test "$enableval" = "yes"
then
  quiet_build=yes
fi])

AC_ARG_ENABLE(gnuplot-checks,
[  --disable-gnuplot-checks     do not run checks for gnuplot],
[if test "$enableval" = "yes"
then
  check_gnuplot=yes
else
  check_gnuplot=no
fi])

AC_ARG_WITH(lucida,
[  --with-lucida           Use Lucida fonts for manual [[CVS only]]],
[ case "$withval" in
  yes)
        FONTREQ="\\RequirePackage[[T1]]{fontenc,lucidabr}"
	use_lucida=yes
        ;;
  *)
        ;;
  esac ],
)
AC_SUBST(FONTREQ)

AC_ARG_WITH(gmake,
[  --with-gmake            Use GNU make explicitly [[default=no]]],
[ case "$withval" in
  yes)
        MAKE="gmake"
        ;;
  *)
        ;;
  esac ],
)
AC_SUBST(MAKE)

AC_ARG_WITH(readline,
[  --with-readline      Build with libreadline support [[default=auto]]],
if test "x${withval}" = "xno"
then
  try_readline=no
else
  try_readline=yes
fi,
try_readline=yes)

AC_ARG_WITH(gnome,
[  --with-gnome         Install gnome support [[default=auto]]],
if test "x${withval}" = "xno"
then
  try_gnome=no
else
  try_gnome=yes
fi,
try_gnome=yes)

gnome_prefix="NA"
AC_ARG_WITH(gnome_install,
[  --with-gnome-install=DIR  where to install gnome-specific files [[default=auto]]],
if test -n "${withval}"
then
  gnome_prefix=${withval}
fi,)

AC_ARG_WITH(gmp,
[  --with-gmp            Use Gnu Multiple Precision library [[default=auto]]],
if test "x${withval}" = "xno"
then
  try_gmp=no
else
  try_gmp=yes
fi,
try_gmp=yes)

AC_ARG_WITH(mpfr,
[  --with-mpfr            Use MPFR library [[default=auto]]],
if test "x${withval}" = "xno"
then
  try_mpfr=no
else
  try_mpfr=yes
fi,
try_mpfr=yes)

AC_ARG_WITH(gtksourceview,
[  --with-gtksourceview   Use installed gtksourceview [[default=auto]]],
if test "x${withval}" = "xno"
then
  try_sourceview=no
else
  try_sourceview=yes
fi,
try_sourceview=yes)

AC_ARG_WITH(x-12-arima,
[  --with-x-12-arima      include X-12-ARIMA support [[default=yes]]],
if test "$withval" = "no"
then
  have_x12a="no"
else
  AC_DEFINE(HAVE_X12A)
  HAVE_X12A=1
fi,
AC_DEFINE(HAVE_X12A)
HAVE_X12A=1)
AC_SUBST(HAVE_X12A)

AC_ARG_WITH(tramo-seats,
[  --with-tramo-seats     include TRAMO/SEATS support [[default=yes]]],
if test "$withval" = "no"
then
  have_tramo="no"
else
  AC_DEFINE(HAVE_TRAMO)
  HAVE_TRAMO=1
fi,
AC_DEFINE(HAVE_TRAMO)
HAVE_TRAMO=1)
AC_SUBST(HAVE_TRAMO)

AC_ARG_WITH(libR,
[  --with-libR            include libR support [[default=auto]]],
if test "$withval" = "no"
then
  try_libR=no
fi,
try_libR=yes)

AC_ARG_WITH(ox,
[  --with-ox              include Ox support [[default=no]]],
if test "$withval" = "yes"
then
  have_ox=yes
  AC_DEFINE(USE_OX)
fi,
have_ox=no)

AC_ARG_WITH(audio,
[  --with-audio           include audio graph support (experimental)],
if test "$withval" = "yes"
then
  AC_DEFINE(HAVE_AUDIO)
  HAVE_AUDIO=1
  AC_SUBST(HAVE_AUDIO)
  have_audio=yes
fi,
have_audio=no)

AC_ARG_WITH(odbc,
[  --with-odbc            include ODBC support (experimental)],
if test "$withval" = "yes"
then
  have_odbc=yes
fi,
have_odbc=no)

AC_PROG_CC

AC_DEFINE_UNQUOTED(OS_TYPE, "$host_os")
AC_PROG_INSTALL

AC_DISABLE_STATIC
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

dnl We want these before the checks, so the checks can modify their values.
test -z "$CFLAGS" && CFLAGS= auto_cflags=1
test -z "$CC" && cc_specified=yes

dnl
dnl if the user hasn't specified CFLAGS, then
dnl   if compiler is gcc, then use -O2 and some warning flags
dnl
if test -n "$auto_cflags"; then
  if test -n "$GCC"; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

AC_PROG_LN_S

AC_CHECK_LIB(m, sin)
AC_CHECK_LIB(c, fopen)

dnl Checks for header files, etc.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(unistd.h dirent.h fnmatch.h byteswap.h sys/times.h)
AC_C_CONST
AC_C_LONG_DOUBLE 
AC_CHECK_SIZEOF(int)
AC_TYPE_SIGNAL 
AC_C_BIGENDIAN 

AC_ARG_VAR(HOSTCC, host compiler (for cross-build))
AC_ARG_VAR(MKLANG, builder for gtksourceview files (cross-build))

dnl try to build mailer plugin?
AC_CHECK_HEADERS(netdb.h sys/socket.h,
	build_mailer="yes" ; AC_DEFINE(ENABLE_MAILER),
	build_mailer="no"
)

if test "$build_mailer" = "yes" ; then
AC_CHECK_TYPE(struct sockaddr_in, 
  AC_DEFINE(HAVE_SOCKADDR_IN, 1, [Do we have sockaddr_in?]),,
  #include <sys/socket.h>
  #include <netdb.h>
)

AC_CHECK_TYPE(struct in_addr, 
  AC_DEFINE(HAVE_IN_ADDR, 1, [Do we have in_addr?]),,
  #include <sys/socket.h>
  #include <netdb.h>
)
fi

dnl
dnl Check for libreadline for use with gretlcli
dnl
if test "$try_readline" = "yes" ; then
   AM_PATH_READLINE(have_readline="yes", have_readline="no")
   if test "$have_readline" = "no" ; then
     READLINE_LIBS=""
     READLINE_CFLAGS=""
   fi
fi

dnl
dnl See if we need to do anything special about "inet_addr"
dnl
INETLIB=''
AC_CHECK_LIB(c, inet_addr,,seek_inet_addr="yes",)
if test "$seek_inet_addr" = yes; then
  AC_CHECK_LIB(bind, inet_addr,INETLIB="-lbind",)
  if test "$INETLIB" != "-lbind"; then
    AC_CHECK_LIB(nsl, inet_addr, INETLIB="-lnsl",
    AC_MSG_ERROR(Can't find a definition of inet_addr),)
  fi
fi
AC_SUBST(INETLIB)

dnl
dnl Is MMAP available?
dnl
AC_MSG_CHECKING([for mmap])
AC_CACHE_VAL(ac_cv_func_mmap_ok,
 [AC_TRY_LINK(
 changequote(<<, >>)dnl
 <<
#include <unistd.h>
#include <sys/mman.h>
#include <sys/stat.h>
#include <fcntl.h>
 >>,
 changequote([, ])dnl
 [mmap((void *)0, 0, PROT_READ, 0, 0, 0);],
 ac_cv_func_mmap_ok=yes,
 ac_cv_func_mmap_ok=no)] )
AC_MSG_RESULT($ac_cv_func_mmap_ok)
if test $ac_cv_func_mmap_ok = yes
then
 AC_DEFINE(HAVE_MMAP)
fi

dnl
dnl Check for zlib
dnl
AC_CHECK_LIB(z, gzopen,have_zlib="yes" ; \
  ZLIB="-lz" ; AC_DEFINE(HAVE_ZLIB),,)
AC_SUBST(have_zlib)

dnl
dnl See if GMP lib is available
dnl
if test x"${try_gmp}" != xno ; then
  AM_PATH_GMP(4.0.1, have_gmp="yes" ; AC_DEFINE(ENABLE_GMP))
fi

dnl
dnl See if MPFR is available
dnl
if test x"${try_mpfr}" != xno ; then
  AM_PATH_MPFR(2.2.0, have_mpfr="yes" ; AC_DEFINE(HAVE_MPFR))
fi

dnl
dnl Check for LAPACK: Apple _will_ be different
dnl
LAPACK_CFLAGS="-I${srcdir}/lib/src"
case $host_os in
  *arwin*) 
    LAPACK_LIBS="-Wl,-framework -Wl,vecLib"
    have_lapack="yes"
    darwin_build="yes"
    AC_DEFINE(OSX_BUILD)
  ;; 
  *)
   AM_PATH_LAPACK(, have_lapack="yes") 
   if test "$have_lapack" = "yes" ; then
     SAVE_LIBS=$LIBS
     LIBS=$LAPACK_LIBS
     AC_CHECK_LIB(lapack, dgejsv_, AC_DEFINE(HAVE_LAPACK_3_2))
     LIBS=$SAVE_LIBS
   fi
  ;;
esac

dnl And while we're at it...
if test "$darwin_build" = "yes" ; then
  CARBONLIB="-Wl,-framework -Wl,Carbon"
  AC_SUBST(CARBONLIB)
  DARWIN_RPATH="@executable_path/../lib"
  AC_SUBST(DARWIN_RPATH)
fi

dnl
dnl Check for FLITE?
dnl
if test "$have_audio" = "yes" ; then
  AM_PATH_FLITE(, 
    have_flite="yes"
    AC_DEFINE(HAVE_FLITE)
  )
fi

dnl
dnl Check for unixODBC?
dnl
if test "$have_odbc" = "yes" ; then
  AM_PATH_ODBC(, 
    have_odbc="yes",
    have_odbc="no"
  )
fi

dnl
dnl Check for gnuplot and its PNG capacity, unless the
dnl user has said not to
dnl
if test "$check_gnuplot" = "no" ; then
  have_gnuplot=yes
  gnuplot_png=yes
  AC_DEFINE(HAVE_GNUPLOT)
  AC_DEFINE(GNUPLOT_PNG)
else
  AC_DEFUN([AC_PROG_GNUPLOT],
  [AC_CHECK_PROG(GNUPLOT,gnuplot,yes)])

  AC_PROG_GNUPLOT
  test x"${GNUPLOT}" = xyes && AC_DEFINE(HAVE_GNUPLOT) 
  if test x"${GNUPLOT}" = xyes ; then
    have_gnuplot=yes
  fi

  gnuplot_png=no
  if test "$build_gui" = "yes" ; then
    AC_MSG_CHECKING(for PNG support in gnuplot)
    echo "set term png" | GNUTERM=dumb `which gnuplot` 2>/dev/null && gnuplot_png=yes
    if test "$gnuplot_png" = yes ; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(GNUPLOT_PNG)
    else
      AC_MSG_RESULT(no)
      echo
      echo "* gretl needs PNG support in gnuplot."
      echo "* The current version of gnuplot is available from www.gnuplot.info"
      exit
    fi
  fi
fi

dnl
dnl Check for latex executable
dnl
AC_DEFUN([AC_PROG_LATEX],
[AC_CHECK_PROG(LATEX,latex,yes)])

AC_PROG_LATEX
test x"${LATEX}" = xyes && AC_DEFINE(HAVE_LATEX)
if test x"${LATEX}" = xyes ; then
  have_latex="yes"
else
  have_latex="no"
fi

dnl
dnl Preliminary check for pkg-config
dnl
if test -z "$PKG_CONFIG"; then
   AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
fi
if test "$PKG_CONFIG" = "no" ; then
   build_gui="no"
else
   have_pkgconfig="yes"
fi

orig_CFLAGS=$CFLAGS

dnl
dnl fftw3
dnl
if test "$PKG_CONFIG" != "no" ; then
   PKG_CHECK_MODULES(FFTW, fftw3,have_fftw3="yes", have_fftw3="no")
fi

dnl
dnl glib 2.0 (needed even if not building GUI)
dnl
if test "$PKG_CONFIG" != "no" ; then
   PKG_CHECK_MODULES(GLIB, glib-2.0, have_glib2="yes", have_glib2="no")
fi

dnl
dnl Check for GTK 2.0
dnl
if test "$build_gui" = "yes" ; then
    PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.4.0,
      CFLAGS="$CFLAGS $GTK_CFLAGS"
      LIBS="$LIBS $GTK_LIBS"
      have_gtk="2.0",
      have_gtk="no"
      build_gui="no"
      echo "GTK not found"     
    )
fi

dnl
dnl Check for GTK 2.10 or higher
dnl
if test "$have_gtk" = "2.0" ; then
    PKG_CHECK_MODULES(GTKPRINT, gtk+-2.0 >= 2.10.0,
      have_gtkprint="yes",
      have_gtkprint="no"      
    )
fi

dnl
dnl Check for installed gtksourceview?
dnl
if test "$have_gtk" = "2.0" && test "$try_sourceview" = "yes" ; then
    PKG_CHECK_MODULES(GTKSOURCEVIEW, gtksourceview-2.0,
      AC_DEFINE(USE_GTKSOURCEVIEW_2)
      have_sourceview="2.0",
      echo "version 2.0 not found"
    )
    if test "$have_sourceview" != "2.0" ; then
       PKG_CHECK_MODULES(GTKSOURCEVIEW, gtksourceview-1.0 >= 1.8.5,
         have_sourceview="1.0",
         echo "will use 1.8.5"
         bundled_sourceview="yes"      
       )
    fi      
fi

dnl Test regex availability for bundled gtksourceview
if test "$bundled_sourceview" = "yes" ; then
     AC_CHECK_FUNCS([re_compile_pattern re_compile_fastmap re_search re_match],
     have_gnu_regex="yes",
     have_gnu_regex="no")
fi

if test x"${MSGFMT}" != x ; then
   build_po="yes"
else
   echo "*** msgfmt not found, can't build message catalogs"
fi

dnl record the results of the above tests
AC_SUBST(build_gui)
AC_SUBST(have_gtk)
AC_SUBST(have_gmp)
AC_SUBST(have_mpfr)
AC_SUBST(have_sourceview)
AC_SUBST(have_gnu_regex)
AC_SUBST(have_audio)
AC_SUBST(have_odbc)
AC_SUBST(build_mailer)
AC_SUBST(build_po)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

dnl Check for gdk-pixbuf support
if test ${have_gtk} = "2.0" ; then
    PKG_CHECK_MODULES(GDK_PIXBUF, gdk-pixbuf-2.0 >= 0.7.0,
        pixbuf_failed="no",
	pixbuf_failed="yes"
   )
fi
if test "${pixbuf_failed}" = "yes" ; then
    echo "* gretl wants but did not find the gdk-pixbuf library for PNG graphs."
    exit
fi

dnl Check for gnome, if we haven't been told not to
if test "$build_gui" = "yes" && test "$try_gnome" = "yes" ; then
    PKG_CHECK_MODULES(GNOME, libgnome-2.0,
       have_gnome="2.0"
       if test "${gnome_prefix}" = "NA" ; then
           gnome_prefix=`pkg-config --variable=prefix libgnome-2.0`
       fi,
       echo "no"
       have_gnome="no"
    )   
    AC_SUBST(have_gnome)
    AC_SUBST(gnome_prefix)
fi

dnl Check for libxml2
if test "$have_gtk" = "2.0" ; then
    PKG_CHECK_MODULES(XML, libxml-2.0 >= 2.5.0)
else 
    XMLCONF=''
    AC_CHECK_PROGS(XMLCONF,xml2-config xml-config,)
    if test x"${XMLCONF}" != x ; then
       XML_CFLAGS=`$XMLCONF --cflags`
       XML_LIBS=`$XMLCONF --libs`
    fi
fi

dnl Check for libxslt (for the helpfiles)
if test "${build_docs}" = "yes" ; then
  PKG_CHECK_MODULES(XSLT, libxslt >= 1.0.15,	
    have_xslt="yes",
    have_xslt="no"
  )
fi

dnl Check for libR (optional)
if test "$try_libR" = "yes" ; then
    PKG_CHECK_MODULES(RLIB, libR,
      AC_DEFINE(USE_RLIB)
      if echo "$RLIB_LIBS" | grep ^-L > /dev/null ; then
        RLIBDIR=`echo $RLIB_LIBS | cut -b 3- | awk '{ print $1 }'`
      else 
        RLIBDIR="/usr/lib"
      fi 
      if test "$darwin_build" = "yes" ; then
        RLIBNAME="libR.dylib"
      else 
        RLIBNAME="libR.so"
      fi
      RLIB="${RLIBDIR}/${RLIBNAME}"
      AC_DEFINE_UNQUOTED(RLIBPATH, "$RLIB") 
      have_libR="yes",
      have_libR="no"
    )
fi

dnl If building docs was requested, see if we can do it
if test "${build_docs}" = "yes" ; then
   if test "$have_xslt" = "no" || test "$have_latex" = "no" ; then
      build_docs="no"
      echo "*** Can't build gretl docs: XSLT and/or LaTeX not found"
   fi
fi

AC_SUBST(build_docs)
AC_SUBST(quiet_build)

dnl record results on various libraries
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)
AC_SUBST(XSLT_CFLAGS)
AC_SUBST(XSLT_LIBS)
AC_SUBST(GDK_PIXBUF_CFLAGS)
AC_SUBST(GDK_PIXBUF_LIBS)
AC_SUBST(RLIB_CFLAGS)
AC_SUBST(RLIB_LIBS)
AC_SUBST(RLIB)

dnl Determine installation prefix
if test "${prefix}" = "NONE" ; then
   prefix="/usr/local"
fi
AC_DEFINE_UNQUOTED(GRETL_PREFIX, "${prefix}")

dnl Create output
CFLAGS="$orig_CFLAGS"
AC_OUTPUT([ 
Makefile 
gretl_sh
gretl-config
gretl.pc 
gui2/Makefile
gui2/debug
gui2/gtksourceview/Makefile
gnome/Makefile
gnome/gretl.keys 
cli/Makefile 
cli/debug
lib/Makefile 
plugin/Makefile
po/Makefile.in 
share/Makefile
share/bcih/Makefile
tests/Makefile
extra/Makefile
osx/Makefile
osx/fixlinks.sh
redhat/gretl.spec
])

if test "${build_docs}" = "yes" ; then
  AC_OUTPUT([
    doc/Makefile
    doc/gretl.sty
    doc/commands/Makefile
    doc/tex/Makefile
    doc/tex_it/Makefile
    doc/tex_es/Makefile
    doc/tex_pt/Makefile
  ])
fi

if test "${have_pkgconfig}" = "no" ; then
echo "
Please install pkg-config and then reconfigure gretl.
pkg-config is available from http://www.freedesktop.org/
"

elif test "${have_zlib}" = "no" ; then
echo "
Please install zlib (compression library) and then reconfigure gretl.
zlib is available via http://www.info-zip.org/pub/infozip/zlib/
"

elif test x"${XML_LIBS}" = x ; then
echo "
Please install libxml2 and then reconfigure gretl.
libxml2 is available from http://xmlsoft.org/
"

elif test "${have_lapack}" = "no" ; then
echo "
Please install lapack and then reconfigure gretl.
Lapack is available from http://www.netlib.org/lapack/
"

elif test "${have_fftw3}" = "no" ; then
echo "
Please install fftw3 and then reconfigure gretl.
fftw3 is available from http://www.fftw.org/
"

elif test "${have_glib2}" = "no" ; then
echo "
Please install glib-2.0 and then reconfigure gretl.
glib-2.0 is available from http://www.gtk.org/
"

else
echo "
Configuration:
  
  Installation path:                      ${prefix}
  Use readline library:                   ${have_readline}
  Use gnuplot for graphs:                 ${have_gnuplot}
  Use PNG for gnuplot graphs:             ${gnuplot_png}
  Use LaTeX for typesetting output:       ${have_latex}
  Gnu Multiple Precision support:         ${have_gmp}
  MPFR support:                           ${have_mpfr}
  FFTW3 support:                          ${have_fftw3}
  Build with GTK version:                 ${have_gtk}
  Use GTK printing apparatus:             ${have_gtkprint}
  Use installed gtksourceview:            ${have_sourceview}
  Build with gnome support:               ${have_gnome}
  Build gretl documentation:              ${build_docs}
  Use Lucida fonts:                       ${use_lucida}
  Build message catalogs:                 ${build_po}
  Gnome installation prefix:              ${gnome_prefix}
  X-12-ARIMA support:                     ${have_x12a}
  TRAMO/SEATS support:                    ${have_tramo}
  libR support:                           ${have_libR}
  Ox support:                             ${have_ox}
  Experimental audio support:             ${have_audio}
  Experimental ODBC support:              ${have_odbc}
  LAPACK libraries:
    ${LAPACK_LIBS}

Now type '$MAKE' to build gretl."
  if test "${build_docs}" = "yes" ; then
    echo "You can also do 'make pdfdocs' to build the PDF documentation."
  fi
  echo
fi

