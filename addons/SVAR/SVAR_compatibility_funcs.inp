# [This file should not need to be include-d anymore, in v1.95.]

# Backward compatibility functions which eventually should become
# obsolete
# (either because they are in extra.zip, or other reasons)


##################################
# getstrings_byindex: gretl 2020b will have the built-in feature
# to slice / fancy-index into an array with a vector

function strings getstrings_byindex(const strings S, const matrix indices)
    # collects the (in general non-contiguous) sub-array
    # indexed by indices (imitate fancy indexing)
    strings out = null
    matrix m = vec(indices) 
    loop i = 1..nelem(m) -q
        out += S[m[i]]
    endloop
    return out
end function


### drawnormwis() was called BayesianDraw
### It will go into extra 0.7 in some incarnation.

function matrices drawnormwis(const matrix iXX, const matrix B,
                               const matrix Sigma, int T[1::])

    # Draw from a standard Normal-(inverse)-Wishart prior 
    # for a multi-equation regression model.
    #
    # iXX:  K x K matrix (X'X)^{-1} (same for all equations!)
    #       (in a VAR context gretl provides this as $xtxinv)
    # B:    matrix of purely data-based estimates (max-lik)
    # Sigma: cross-equation covariance matrix of innovations
    # T:    number of observations

    K = rows(B) 
    N = cols(B) # how many equations 

    # some checks 
    if K != rows(iXX)
        funcerr "Coeff and data matrix dims don't match"
    elif N != rows(Sigma)
        funcerr "Coeff and Cov matrix dims don't match"
    endif

    matrix Sigma_draw = iwishart(Sigma*T, T)
    matrix V = Sigma_draw ** iXX
    matrix C = cholesky(V)
    matrix B_draw = vec(B)

    B_draw += C * mnormal(K * N, 1)
    B_draw = mshape(B_draw, K, N)

    return defarray(B_draw, Sigma_draw)
end function

###
# In gretl 2020a there will be 'instrings' with the same 
# functionality as this strpos_allin
# (I hope it also returns a column vector...!)

function matrix strpos_allin(strings S, string search_pattern)
    # Function to determine the numerical position(s) of a 
    # string in an array

    matrix ret = {}
    loop i=1..nelem(S) -q
        if S[i] == search_pattern
            ret |= i
        endif
    endloop
    return ret 
end function


###
# A very similar functionality is in splitfname in extra 0.6

function strings basename(string fn)
    string base = regsub(fn, "(.*)\.([^\.]*)", "\1")
    string ext = fn + (strlen(base) + 1)
    return defarray(base, ext)
end function



###
### the drill() function is in extra >= 0.6
###

function matrix drill(const matrices x,
                      matrix rowspec[null],
                      matrix colspec[null])

    # This function "drills through" a matrix array and returns a matrix;
    # for example, drill(x, 2, 3) returns a vector with the [2,3] elements
    # of all matrices in the x array. "0" means "all".
    #
    # NOTA BENE: all matrices must be the same size

    matrix ret = {}
    n = nelem(x)

    if n == 0
        return ret
    endif

    ### check sizes

    nr = rows(x[1])
    nc = cols(x[1])
    
    same_dim = 1
    loop i = 2 .. n --quiet
        same_dim = same_dim && (rows(x[i]) == nr) && (cols(x[i]) == nc) 
        if !same_dim
            printf "Error: Inconsistent dimensions (not all matrices are the same size)\n"
            return ret
        endif
    endloop

    ### process specs

    if !exists(rowspec) 
        matrix rs = seq(1, nr)'
    else
        if rowspec[1] == 0
            matrix rs = seq(1, nr)'
        else
            matrix rs = vec(rowspec) # force to column
        endif
    endif

    if !exists(colspec)
        matrix cs = seq(1, nc)'
    else
        if colspec[1] == 0
            matrix cs = seq(1, nc)'
        else
            matrix cs = vec(colspec) # force to column
        endif
    endif
        
    ### check for multiple or illegal specs
    
    scalar nrspec = rows(rs)
    scalar ncspec = rows(cs)

    if xmin(nrspec, ncspec) > 1
        printf "Error: you canâ€™t have multiple row and column specs\n"
        return ret
    endif
    
    if minc(rs|cs) < 0
        printf "Error: negative spec not allowed\n"
        return ret
    endif
    
    if maxc(rs) > nr
        printf "Error: incorrect row spec (matrices have %d rows, but %d wanted)\n", nr, maxc(rs)
        return ret
    endif
    
    if maxc(cs) > nc
        printf "Error: incorrect col spec (matrices have %d columns, but %d wanted)\n", nc, maxc(cs)
        return ret
    endif
    
    ### do the actual drilling

    if nrspec == 1 
        ret = flatten(x)[rs,]
        ret = transp(mshape(ret, nc, n))
        ret = ret[,cs]
    elif ncspec == 1 
        ret = flatten(x,1)[,cs]
        ret = mshape(ret, nr, n)
        ret = ret[rs,]
    endif
    
    return ret
end function
 
# --------------------------------

##
## names2indices will go into extra 0.7 under the name 'multi_instrings'.
# However, the values() transformation will be skipped there.
# So to replace names2indices() use values(multi_instrings()).
#
# Also - passing an empty 'names' array will return an empty result
# in multi_instrings instead of the entire sequence of indices here, 
# so watch out.
## 

function matrix names2indices(strings fromwhich, strings names)

    matrix which = {}
    if !nelem(names) # default, all
        which = seq(1, nelem(fromwhich))

    else
        loop n = 1..nelem(names) -q
            which |= strpos_allin(fromwhich, names[n]) 
        endloop 
        which = values(which)
    endif
    return which # may still be empty
end function 
        
