# (function splitfname comes from the extra addon)

####

function scalar check_bounds(int s, int v, int n)
    # s is the shock number, m is the variable number, n is the
    # system size

    scalar ret = 0
    if abs(s)>n || s==0
        # the number of the shock is greater than the system size
        # however, negative s is allowed and means "flip the shock"
        ret = 1

    elif v>n || v<1
        # non-existent variable
        ret = 2
    endif

    return ret
end function

/* ---------------------------------------------------------------------- */
/* --- IRF core graphing function --------------------------------------- */
/* ---------------------------------------------------------------------- */

function string IRFgrph(const matrix IRFmat, int snum, int vnum, scalar scale, \
                        string sname, string vname, int keypos[0:2:1], \
                        matrix *cumul[null],  bundle *boot[null], \
                        int bc[0:2:0], int whichdraw[0::0])

    # bc: bias correction choice
    # (snum and sname belong together, as do vnum and vname, so it's a bit
    #  unfortunate that the scale arg is positioned in-between. But we need to
    #  have both the number and the string because [i] the number can be negative
    #  to signal flipping, and [ii] we avoid passing the whole bundle around here.)
    # (The number of args here is horrible, but fortunately this is a private
    #  function.)

    errorif( whichdraw && (exists(boot) || bc), \
      "cannot have SR/whichdraw and bootstrap options together")

    flip = snum < 0
    snum = abs(snum)
    n = round(sqrt(cols(IRFmat)))
    bootrep = exists(boot) ? boot.rep : 0 # didn't work due to gretl bug+

    # anything to cumulate?
    cumulate = exists(cumul) ? sum(cumul .= vnum) > 0 : 0

    tmpfname = int(1000 * muniform(1))
    string tmpfile = $windows ? sprintf("%s\\irf%d.gp", $dotdir, tmpfname) : \
      sprintf("%s/irf%d.gp", $dotdir, tmpfname)

    k = (snum-1) * n + vnum

    h = rows(IRFmat)
    matrix x = flip ? -IRFmat[,k]./scale : IRFmat[,k]./scale	# matrix?
    if bootrep > 0
        matrix hicb = flip ? -boot.lo_cb : boot.hi_cb	# matrix?
        matrix locb = flip ? -boot.hi_cb : boot.lo_cb
        matrix mdn  = flip ? -boot.mdns  : boot.mdns
        matrix locb = locb[,k] ./ scale
        matrix hicb = hicb[,k] ./ scale
        matrix mdn  = mdn[,k]  ./ scale
        scalar miny = minc((locb | x))
        scalar maxy = maxc((hicb | x))
    else
        scalar miny = minc(x)
        scalar maxy = maxc(x)
    endif

    miny = miny>0 ? 0 : miny
    maxy = maxy<0 ? 0 : maxy

    set force_decpoint on

    ## write the raw gnuplot code

    outfile "@tmpfile"
        printf "set yrange [%g:%g]\n", miny*1.05, maxy*1.05
        printf "set xrange [%g:%g]\n", 0, (h-1)*1.05
        printf "set xzeroaxis\n"

        string l_sname = sname=="" ? sprintf("%d", snum) : sname
        string l_vname = vname=="" ? sprintf("%d", vnum) : vname

        printf "set title \"IRF: %s -> %s", l_sname, l_vname

        if bc == 1
            printf "; bias-correction = partial"
        elif bc == 2
            printf "; bias-correction = full"
        elif whichdraw
            printf " (draw %d)", whichdraw
        endif

        if cumulate > 0
            printf " (cumulated)"
        endif

        printf "\"\n"

        if keypos == 0
            printf "set key off\n"
        elif keypos == 1
            printf "set key outside\n"
        elif keypos == 2
            printf "set key below\n"
        endif

        if bootrep > 0
            printf "set style fill solid 0.125\n"
            printf "plot '-' using 1:2:3 w filledcurve t "
            printf "'Bstrap %d%% CI', \\\n", floor(100 * boot.alpha)
            printf "'-' w l lw 1 lc 1 t 'Bstrap median', \\\n"
            printf "'-' w l lw 2 lc -1 t 'IRF'\n"
            loop i = 1..h
                printf "%d\t%g\t%g\n", i-1, locb[i,], hicb[i,]
            endloop
            printf "e\n"
            loop i = 1..h
                printf "%d\t%g\n", i-1, mdn[i,]
            endloop
            printf "e\n"
        else
            printf "plot '-' w l lw 2\n"
        endif

        loop i = 1..h
            printf "%d\t%g\n", i-1, x[i]
        endloop
        printf "e\n"
    end outfile
    set force_decpoint off # in gretl >=2021b this is redundant

    return tmpfile
end function

/* ---------------------------------------------------------------------- */
/* --- IRF public graphing functions ------------------------------------ */
/* ---------------------------------------------------------------------- */

function void IRFsave(string outfilename, bundle *obj, int snum,
                      int vnum, int keypos[0:2:1], int drawix[0::0])

    # negative snum is allowed and means to flip the shock

    # copy and prepare some input
    string tmpfile, tmpout
    n = obj.n
    whichdraw = drawix ? drawix : obj.bestdraw

    if obj.type == 10	# SR
        errchkSRhisto(&obj, drawix)
        putIrf_to_accdraw(&obj, whichdraw)
        matrix IRFmat = obj.acc_draws[whichdraw].IRFs
    else
        matrix IRFmat = obj.IRFs
    endif

    scale = 1	# possibly changed later
    if obj.normalize == 1
        matrix tmp = mshape(IRFmat[1,], n, n)
    endif

    if obj.nboot
        boot = obj.bootdata	# bundle?
        bc = boot.biascorr
    endif

    scalar sfrom sto vfrom vto
    is_srange = range(snum, n, &sfrom, &sto)
    is_vrange = range(vnum, n, &vfrom, &vto)

    # do checks
    loop snum = sfrom .. sto
        loop vnum = vfrom .. vto
            err = check_bounds(snum, vnum, n)
            if err
                break
            endif
        endloop
    endloop

    errorif(err == 1, sprintf("Shock number %d out of bounds\n", abs(snum)))
    errorif(err == 2, sprintf("Variable number %d out of bounds\n", abs(vnum)))

    # cycle through all (selected) shocks and variables
    loop snum = sfrom..sto

        string sn = obj.snames[abs(snum)]
        # normalization / scaling
        if obj.normalize == 1
            scale = tmp[abs(snum), abs(snum)]
        endif

        loop vnum = vfrom .. vto
            string yn = obj.Ynames[vnum]

            # produce plots
            if obj.ncumul == 0
                if obj.nboot == 0
                    tmpfile = IRFgrph(IRFmat, snum, vnum, scale, sn, yn, keypos, \
                      null, null, , whichdraw)
                else
                    tmpfile = IRFgrph(IRFmat, snum, vnum, scale, sn, yn, keypos, \
                      null, &boot, bc, whichdraw)
                endif
            else
                matrix cumul = obj.cumul
                if obj.nboot == 0
                    tmpfile = IRFgrph(IRFmat, snum, vnum, scale, sn, yn, keypos, \
                      &cumul, null, , whichdraw)
                else
                    tmpfile = IRFgrph(IRFmat, snum, vnum, scale, sn, yn, keypos, \
                      &cumul, &boot, bc, whichdraw)
                endif
            endif

            if outfilename=="display" || (sfrom == sto && vfrom == vto)
                # (single plot, no indices)
                tmpout = outfilename
            else
                strings be = splitfname(outfilename)[2:3]	# gives 2-elem array, was basename
                if is_vrange	 # several v indices
                    if is_srange # several s indices
                        tmpout = sprintf("%s_%d%d.%s", be[1], snum, vnum, be[2])
                    else
                        tmpout = sprintf("%s_%d.%s", be[1], vnum, be[2])
                    endif
                elif is_srange 	# several s indices
                    tmpout = sprintf("%s_%d.%s", be[1], snum, be[2])
                else
                    funcerr "shouldn't happen"
                endif
            endif
            gnuplot --input="@tmpfile" --output="@tmpout"
        endloop
    endloop

end function

function void IRFplot(bundle *obj, int snum[1], int vnum[1],
                      int keypos[0:2:1], int drawix[0::0])

    IRFsave("display", &obj, snum, vnum, keypos, drawix)

end function

/* ---------------------------------------------------------------------- */
/* --- FEVD core graphing function -------------------------------------- */
/* ---------------------------------------------------------------------- */

function string FEVDgrph(const matrix FEVDmat, scalar v,
                         string vname, strings snames, int keypos[0:2:1],
                         string titletail[null])

    # Final arg 'titletail' is a hack originally meant to put the draw number
    # in the output in the SR case. (In principle it can be anything.)

    if !exists(titletail)
        string titletail = ""
    endif

    n = round(sqrt(cols(FEVDmat)))
    h = rows(FEVDmat) - 1

    scalar tmpfname = int(10000 * muniform(1))
    if $windows
        string datfile = sprintf("%s\\fevd%d.txt", $dotdir, tmpfname)
        string tmpfile = sprintf("%s\\fevd%d.gp", $dotdir, tmpfname)
    else
        string datfile = sprintf("%s/fevd%d.txt", $dotdir, tmpfname)
        string tmpfile = sprintf("%s/fevd%d.gp", $dotdir, tmpfname)
    endif

    matrix sel = (v-1)*n + seq(1,n)
    set force_decpoint on

    outfile "@tmpfile"
        if keypos == 0
            printf "set key off\n"
        elif keypos == 1
            printf "set key outside\n"
        elif keypos == 2
            printf "set key below\n"
        endif
        printf "set yrange [0:100]\n"
        printf "set xrange [%g:%g]\n", 0, h
        printf "set xzeroaxis\n"

        printf "set title \"FEVD for %s%s\"\n", vname, titletail
        printf "set style fill solid 0.25\n"
        printf "set style histogram rowstacked\n"
        printf "set style data histogram\n"

        loop i = 1..n
            string sname = snames[i]

            if i == 1
                printf "plot '%s' using 2 t '%s', \\\n", datfile, sname
            elif i == n
                printf "\t'' using %d t '%s'\n", i+1, sname
            else
                printf "\t'' using %d t '%s', \\\n", i+1, sname
            endif
        endloop
    end outfile

    outfile "@datfile"
        printf "%12.4f\n", seq(0, h)' ~ 100*FEVDmat[,sel]
    end outfile

    set force_decpoint off # not needed in gretl >= 2021b

    return tmpfile
end function


/* ---------------------------------------------------------------------- */
/* --- FEVD public graphing functions ----------------------------------- */
/* ---------------------------------------------------------------------- */

function void FEVDsave(string outfilename, bundle *obj, int vnum[0], \
                       int keypos[0:2:1], int drawix[0::0])

    scalar n = obj.n
    scalar vfrom vto
    is_vrange = range(vnum, n, &vfrom, &vto)
    matrix Fmat = FEVD(&obj, drawix)

    # prepare title amendment in SR case
    string titletail = ""
    if obj.type == 10
        whichdraw = drawix ? drawix : obj.bestdraw
        titletail = sprintf(" (draw %d)", whichdraw)
    endif

    if outfilename == "display"
        string tmpout = "display"
    else
        strings be = splitfname(outfilename)[2:3]
        string tmpout = sprintf("%s.%s", be[1], be[2]) # might be overriden
    endif

    loop vnum = vfrom..vto
        errorif(vnum < 1 || vnum > n, \
          sprintf("Variable number %d out of bounds\n", vnum))

        if exists(be) && vfrom != vto
            tmpout = sprintf("%s_%d.%s", be[1], vnum, be[2])
        endif

        string tmpfile = FEVDgrph(Fmat, vnum, obj.Ynames[vnum], obj.snames, \
          keypos, titletail)
        gnuplot --input="@tmpfile" --output="@tmpout"

    endloop
end function

function void FEVDplot(bundle *obj, int vnum[0], int keypos[0:2:1],
                       int drawix[0::0])

    FEVDsave("display", &obj, vnum, keypos, drawix)

end function

/* ---------------------------------------------------------------------- */
/* --- HD public graphing functions ------------------------------------- */
/* ---------------------------------------------------------------------- */


function string make_xtics(bundle calinfo, scalar VARorder, scalar ntics[12])
    string ret = "set xtics ("
    
    scalar pd = calinfo.pd
    scalar T = calinfo.t2 - (calinfo.t1 + VARorder) + 1
    matrix tt = (VARorder - 1) + seq(1, T)'
    omaj = calinfo.limitobs[1,1] + floor(tt/pd)
    if cols(calinfo.limitobs) > 1
        # not yearly
        if calinfo.pd == 4 || calinfo.pd == 12
            # quarterly/monthly
            omin = 1 + (calinfo.limitobs[1,2] + (tt-2)) % pd
        elif contains(calinfo.pd, {5,6,7})
            # daily
            d0 = calinfo.limitobs[1,]
            y = d0[1]
            m = d0[2]
            d = d0[3]
            ed = epochday(y, m, d)
            wd = weekday(y, m, d)
            
            omin = zeros(T, 1)
            i = 1
            loop while i<=T
                wd = weekday(y, m, d)
                isgood = (pd == 7) || (pd == 6 && wd > 0) || (pd == 5 && contains(wd, seq(1,5))) 
                if isgood
                    omaj[i] = y
                    omin[i] = m
                    i++
                endif
                cur = isodate(ed++)
                d = cur % 100
                cur = (cur-d)/100
                m = cur % 100
                y = (cur-m)/100
            endloop
        endif
    endif
    
    width = floor(T/ntics)
    head = floor((width + (T % ntics)) / 2) 

    j = head
    loop i = 1 .. ntics
        if pd == 1 || pd == 52 
            string xtic = sprintf("%d", omaj[j])
        elif calinfo.pd == 4 || calinfo.pd == 12 || contains(calinfo.pd, {5,6,7})
            string xtic = sprintf("%d:%d", omaj[j], omin[j])
        endif
        ret ~= sprintf("\"%s\" %d,", xtic, j)
        j += width
    endloop

    ret[end] = ")"
    return ret
end function

function void HDsave(string outfilename, bundle *obj, int vnum[0],
                     int drawix[0::0])

    # Interpret vnum==0 as meaning "all 1..n"
    scalar n = obj.n
    scalar vfrom vto
    is_vrange = range(vnum, n, &vfrom, &vto)

    if outfilename == "display"
        string tmpout = "display"
    else
        strings be = splitfname(outfilename)[2:3]
        string tmpout = sprintf("%s.%s", be[1], be[2]) # might be overriden
    endif

    OLD = 0
    loop vnum = vfrom..vto
        errorif(vnum < 1 || vnum > n, \
          sprintf("Variable number %d out of bounds\n", vnum))

        # prepare extended plot title, only for SR
        whichdraw = drawix ? drawix : obj.bestdraw
        string drawinfo = obj.type == 10 ? sprintf(" (draw %d)", whichdraw) : ""

        list HDData = SVAR_hd(&obj, vnum, drawix)

        if exists(be) && vfrom != vto	# no display, multiple plots
            tmpout = sprintf("%s_%d.%s", be[1], vnum, be[2])
        endif

        string gnam = sprintf("%s (stoch. component)", obj.Ynames[vnum])

        if OLD
            HDData -= HDData[1] # take away deterministic component
            loop i = 1..nelem(HDData)
                string sn = obj.snames[i]
                setinfo HDData[i] --graph-name="@sn"
            endloop

            series tmpvar = sum(HDData)
            setinfo tmpvar --graph-name="@gnam"
            # (put the line with the target var (stoch.comp) _last_
            # such that the shock colors match those in FEVD
            HDData += tmpvar

            plot HDData
                option time-series
                option single-yaxis
                option with-boxes
                option with-lines=tmpvar
                printf "set title \"HD for %s%s\"", obj.Ynames[vnum], drawinfo
            end plot --output="@tmpout"
        else
            
            # more fashionable (=Dynare-like) style
            mat = {HDData}[obj.p+1:,]

            n = cols(mat)
            # we use column 1 for the "stochastic only" variable
            mat[,1] = sumr(mat[,2:])

            ret = ""
            set force_decpoint on
            
            outfile --buffer=ret --quiet
                printf "set xlabel ''\n"
                printf "set xzeroaxis\n"
                printf "set key left top\n"
                printf "set style histogram rowstacked\n"
                printf "set key outside below\n"
                printf "set style fill solid 0.5\n"
                printf "\n"
                printf "%s\n", make_xtics(obj.calinfo, obj.p)
                printf "\n"
                printf "set title \"HD for %s%s\"\n\n", obj.Ynames[vnum], drawinfo

                printf "$data << EOD\n"
                printf "%12.5g", mat
                printf "EOD\n\n"

                printf "plot"
                loop i = 2 .. n
                    printf " $data u %d w hist t \"%s\", \\\n", i, obj.snames[i-1]
                endloop

                printf " $data using 1 w lines lc \"black\" lw 1.5 t '%s'\n", gnam
            end outfile
            gnuplot --inbuf=ret --output="@tmpout"
        endif

    endloop
end function

function void HDplot(bundle *obj, int vnum[0], int drawix[0::0])

    HDsave("display", &obj, vnum, drawix)

end function

/* ---------------------------------------------------------------------- */
/* --- GUI hook --------------------------------------------------------- */
/* ---------------------------------------------------------------------- */

function void GUI_plot(bundle *b, int ptype[0:2:0] \
                       "Plot type" {"IRF", "FEVD", "Historical decomposition"})

    if ptype == 0
        IRFplot(&b, 0, 0, (b.nboot ? 2 : 0)) # all in one go / no key if no bands

    elif ptype == 1
        FEVDplot(&b, 0)

    elif ptype == 2
        HDplot(&b, 0)
    endif

end function
