include extra_matrix.inp
include extra_string.inp
include extra_withdata.inp


function matrix WPtable (int n[4::12] "number of trials")
/*
   Upper and Lower Percentiles of the Wilcoxon Signed
   Rank Statistic W
   Returns a 3 x M matrix of critical values:
   (M differs for each n.)
   1st row: lower crit. vals, always ascending
   2nd row: upper crit. vals, always descending
   3rd row: corresponding P(X <= lower crit.val)
   ( equal to P(X >= upper crit.val) )
    */
    if n == 4
        return {0, 1} | {10, 9} | {0.062, 0.125}
    elif n == 5
        return seq(0, 3) | seq(15, 12) | {0.031,0.062,0.094,0.156}
    elif n == 6
        return seq(0, 5) | seq(21, 16) | {0.016,0.031,0.047,0.078,0.109,0.156}
    elif n == 7
        return seq(0, 7) | seq(28, 21) | {0.008,0.016,0.023,0.039,0.055,0.078,0.109,0.148}
    elif n == 8
        return seq(0, 9) | seq(36, 27) | {0.004,0.008,0.012,0.020,0.027,0.039,0.055,0.074,0.098,0.125}
    elif n == 9
        return seq(1, 12) | seq(44, 33) | {0.004,0.006,0.010,0.014,0.020,0.027,0.037,0.049,0.064,0.082,0.102,0.125}
    elif n == 10
        return seq(3, 16) | seq(52, 39) | {0.005,0.007,0.010,0.014,0.019,0.024,0.032,0.042, 0.053,0.065,0.080,0.097,0.116,0.138}
    elif n == 11
        return seq(5, 20) | seq(61, 46) | {0.005,0.007,0.009,0.012,0.016,0.021,0.027,0.034, 0.042,0.051,0.062,0.074,0.087,0.103,0.120,0.139}
    elif n == 12
        return seq(7, 24) | seq(71, 54) | {0.005,0.006,0.008,0.010,0.013,0.017,0.021,0.026, 0.032,0.039,0.046,0.055,0.065,0.076,0.088,0.102,0.117,0.133}
    endif
end function


function matrix truncnorm (int n[1::] "Number of random variables",
                           scalar m[0] "Mean value",
                           scalar sigma[0::1] "Std. deviation",
                           scalar below "left truncation value",
                           scalar above "right truncation value")
    /* Generates truncated normal random values. Set 'below' and/or 'above'
    to NA to skip. Returns a column vector. */

    if !ok(m)
        funcerr "You must pass a valid (non-nan) value for the mean."
    endif

    scalar l = ok(below) ? cnorm((below - m)/sigma) : 0
    scalar r = ok(above) ? cnorm((above - m)/sigma) : 1
    matrix u = l + (r - l) .* muniform(n, 1)

    return invcdf(z, u) .* sigma + m
end function



function matrix scores2x2 (matrix in,
                           bool verbose[0])
    /*
       Computes some standard score measures for a 2x2
       contingency table of the form:
       Observed
       1      0
       --------------
       Predicted  1 | h(its)  f(alse)
       0 | m(iss)  z(eros)
       and n = h + f + m + z (total observations)
       1) POD / prob of detection = h / (h + m)
       2) POFD / prob of false detection = f / (f + z)
       3) HR / hit rate = (h + z) / n
       4) FAR / false alarm rate = f / (h + f)
       5) CSI / critical success index = h / (h + f + m)
       6) OR / odds ratio = h*z / (f*m)
       7) BIAS / bias score = (h + f) / (h + m)
       8) TSS / true skill stat = POD - POFD
       9) HSS / Heidke skill score = 2 * (h*z - f*m) /
       ( (h+m)*(m+z) + (h+f)*(f+z) )
       10) ETS / equitable threat score = (h*z - f*m) /
       ( (f+m)*n + (h*z - f*m) )
       11) PRC / precision = h / (h + f)
       12) FSC / F-Score = 2 * (PRC*POD) / (PRC + POD)
       The TSS is also known as the Hanssen-Kuipers score,and is = h / (h+m) - f / (f+z).
       The F-Score can also be expressed as 2 * h / (1 + h + m).
       The input is always sanitized by taking the upper 2x2 part,
       using absolute values, and integer-ization.
       Warnings are issued if verbose == 1.
    */
    # (Skip the checks for efficiency if not verbose)
    if verbose
        if rows(in) != 2 || cols(in) != 2
            print "Warning: Discarding input beyond 2x2"
        endif
        if minr(minc(in)) < 0
            print "Warning: Converting negative entries"
        endif
        if sum(abs( in - int(in) )) > 1e-6
            print "Warning: Rounding non-integers"
        endif
    endif
    in = int(abs( in[1:2, 1:2] ))
    scalar h = in[1,1]
    scalar m = in[2,1]
    scalar f = in[1,2]
    scalar z = in[2,2]
    scalar n = sum(in)
    h_m = h + m
    f_z = f + z
    h_z = h + z
    h_f = h + f
    m_z = m + z
    f_m = f + m
    hz = h * z
    fm = f * m
    hzMfm = hz - fm
    POD = h / h_m
    POFD = f / f_z
    HR = h_z / n
    FAR = f / h_f
    CSI = h / (h_f + m)
    OR = hz / fm
    BIAS = h_f / h_m
    TSS = POD - POFD
    HSS = 2 * hzMfm / ( h_m * m_z + h_f * f_z )
    ETS = hzMfm / ( f_m * n + hzMfm )
    PRC = h / h_f
    FSC = 2 * h / (1 + h_m)
    matrix out = {POD; POFD; HR; FAR; CSI; OR; BIAS; TSS; HSS; ETS; PRC; FSC}
    rnameset(out, "POD POFD HR FAR CSI OR BIAS TSS HSS ETS PRC FSC")
    return out
end function

function matrix WSRcritical (int n[4::] "number of trials",
                             scalar prob[0:1:0.05] "two-sided prob mass",
                             bool forcenorm[0] "always use normal approx")
    /* Tries to find the critical values (low/hi) where the
       two-sided area to the outside is as close as possible
       to the given prob.
       (Note that "outside" means including the critical values
       themselves in the exact/discrete case.)
       If we end up in the interior region not covered by the
       exact table (for prob far away from 0 and also from 1), we fall back to the normal approx.
       Returned is col vector {low; hi; epv}, where epv is the actual probability mass
       (close to prob but not equal in general for small samples).
       'low' and 'hi' can be non-integers in the normal
       approximation case.
       The normal approximation instead of the exact table values can be
       enforced with the 'forcenorm' argument.
    */
    approxtol = 0.05
    if n <= 12 && !forcenorm
        matrix lohiP = WPtable(n)
        lohiP[3, ] *= 2	# from one-sided to two-sided pv
        # find the min deviation from the p-value
        scalar ix = iminr(abs( lohiP[3, ] - prob ))
        # now check if we are too far in the interior region
        # where the exact table doesn't apply and the result
        # would be misleading;
        if abs(lohiP[3, ix] - prob ) <= approxtol
            return lohiP[, ix]
        else
            forcenorm = 1 # fall back to normal approx.
        endif
    endif
    if n > 12 || forcenorm # normal approx.
        Wmean = n * (n + 1) / 4
        Wsigma = sqrt(Wmean * (2 * n + 1) / 6)
        cv = critical(N, prob/2) # upper critical value
        hi = cv * Wsigma + Wmean
        lo = Wmean - (hi - Wmean) # symmetric lower
        return {lo; hi; prob}
    endif
end function

function scalar WSRpvalue (int n[4::] "number of trials",
                           scalar W[0::] "W test stat",
                           bool forcenorm[0] "always use normal approx")
    /* We return P(X >= W), _not_ strict inequality!
       (in contrast to an earlier version!)
       In the interior region not covered by the exact table, the true value is
       >= 12.5% according to WPtable, so typically based on such a p-value
       H0 would not be rejected.
       We fall back to the normal approximation in this region.
       In the extreme outer regions not explicitly covered by the table, the deviation
       from 0 or 1 will be smaller than 0.5% = 0.005. We return
       values 1e-3 or 1 - 1e-3 as an approximation here.
       The normal approximation instead of the exact table values can be
       enforced with the 'forcenorm' argument.

       Source of the table: Wilfrid J Dixon and Frank J. Massey, Jr.,
       Introduction to Statistical Analysis, 2nd ed. (New York: McGraw-Hill, 1957),
       pp. 443-444.
    */
    # input check
    if W > n * (n+1) / 2
        printf "Warning: stat %d out of range for %d trials!\n", W, n
        return NA
    endif
    if W != int(W)
        # for a non-integer input (only possible for bindings?)
        # we always fall back to the normal approx
        forcenorm = 1
    else
        W = int(W)	# should be redundant, but who knows (numerically)
    endif
    approxdiff = 1e-3
    if n <= 12 && !forcenorm
        if W == 0	# trivial but possible case
            return 1
        endif
        matrix lohiP = WPtable(n)
        if W > lohiP[1, cols(lohiP)]  &&  W < lohiP[2, cols(lohiP)]
            # (> max left, or < min right) no exact values possible,
            # fall back to normal approx
            forcenorm = 1
        elif W <= lohiP[1, 1]	# extreme lower tail
            return 1 - approxdiff
        elif W > lohiP[2, 1]	# extreme upper tail
            return approxdiff
        elif W <= lohiP[1, cols(lohiP)] # covered in lower tail
            # get the <= W-1 prob
            scalar P = selifc( lohiP[3,], lohiP[1,] .= (W - 1) )
            # convert to > (W-1) ==: >= W prob
            return 1 - P
        elif W >= lohiP[2, cols(lohiP)]	# covered in upper tail
            scalar P = selifc( lohiP[3,], lohiP[2,] .= W )
            return P
        endif
    endif # exact values
    if n > 12 || forcenorm # normal approx.
        Wmean = n * (n + 1) / 4
        Wsigma = sqrt(Wmean * (2 * n + 1) / 6)
        Wstar = (W - Wmean) / Wsigma
        return pvalue(N, Wstar)
    endif
end function


function matrix onemode (matrix v "Vector of values")
  /* Returns one mode (modal value) of the input data v. If that is multi-modal,
  details of internal computer arithmetic can influence which of the modes
  is actually found. Returns a 2-element column vector with the modal value
  and its absolute frequency. Returns a 2-element column vector with the
  modal value and its absolute frequency. If v is an empty matrix (comprises
  only nan values) a 1x1 matrix with nan is returned. */

    matrix ret = mshape(NA, 1, 1)
    matrix v = vec(v)

    if sum(ok(v)) == 0
        printf "\nError: No valid input data.\n"
    else
        matrix E = ecdf(v)
        matrix howmuch = diff(0 | E[,2])[2:]    # make sure 1st is also diffed
        /* if multi-modal selection is not clear (finite precision digital
        arithmetic) */
        matrix where = imaxc(howmuch)

        matrix ret = E[where, 1] | howmuch[where]
        rnameset(ret, "Mode frequency")
    endif

    return ret
end function


function matrix drawbootres(const matrix U,
                            bundle bparams[null] "options",
                            int bootcode[1:5:1] "bootstrap type code" \
                            {"resampling", "wildN", "wildR", "wildM", \
							 "moving blocks"})

    /*
       Construct a new draw of residuals for bootstrapping, where U
       is a Txn matrix.
       U can be original residuals or can be some pre-processed input.
       (See for example the prepres() function in SVAR - the pre-processing is
       not done here to avoid doing it repeatedly.)

       The bootstrap type can be chosen with the "btypestr" string member of
       the bparams bundle, or by the integer function argument "bootcode"; the 
       bundle-based spec takes precedence.
       Currently supported options:
       code | bparams.btypestr string spec| explanation
       
       1    | any word starting with "re" | traditional residual resampling 
            |                             |  (using gretl's resample())
       2    | "wildN" or "wild"           | wild bootstrap, normal distrib.
       3    | "wildR"                     | wild bootstrap, Rademacher distrib.
       4    | "wildM"                     | wild bootstrap, Mammen distrib.
       5    | any word starting with "mov"| RBMBB Br√ºggemann, Jentsch, Trenkler 
            |  or "MBB"                   |  (2016) 

       For the moving-blocks bootstrap (5) the block length can be set with the
       bundle scalar member bparams.movblocklen. Default is T/10 (truncated).
       
       Other bootstrap types may be added in the future.
       (The U input could also be empty then, e.g. for a purely parametric-
       distribution bootstrap.)        
    */
    
    ## process the boottype specification
    # (this could perhaps be done more professionally with bcheck()...)
    
    if exists(bparams.btypestr)
        # The boottype spec is optional, because otherwise the int arg bootcode
        # is used automatically.
         
        errorif(typeof(bparams.btypestr) != 4, \
            "btypestr element must be string")
            
        string bt = tolower(bparams.btypestr)
        if bt[1:2] == "re"
            bootcode = 1
        elif bt == "wild" || bt == "wildn"
            bootcode = 2
        elif bt == "wildr"
            bootcode = 3
        elif bt == "wildm"
            bootcode = 4
        elif bt[1:3] == "mov" || bt[1:3] == "mbb"
            bootcode = 5
        else
            printf "Unknown btypestr option, using bootcode %d.\n", bootcode
        endif
    endif    
        
	# process extra input
	bl = 0 	# signals default data-based choice
    if exists(bparams.moveblocklen) && bootcode == 5
        bl = round(bparams.moveblocklen)
        errorif(bl < 1, "moving block length must be 1 or larger")     
	endif

	## standard
    if bootcode == 1	
        return resample(U)
	endif 

	T = rows(U)

	## wild 
    if bootcode <= 4	
        
        if bootcode == 2
            # Normal
            matrix w = mnormal(T)
        elif bootcode == 3
            # Rademacher
            matrix w = muniform(T) .< 0.5 ? 1 : -1
        elif bootcode == 4
            # Mammen
            scalar s5 = sqrt(5)
            scalar p = (0.5/s5) * (s5 + 1)
            matrix w = 0.5 + (muniform(T) .< p ? -s5/2 : s5/2)
        endif

        return U .* w

	## residual-based moving blocks
	elif bootcode == 5
	    errorif(T < 3, "drawbootres: moving blocks needs at least 3 obs")
		if bl >= T
        	print "Warning: block length too large, falling back to auto"
			bl = 0
		endif
    	if bl == 0	# use default
        	bl = xmax(2, floor(T/10)) 
		endif       
    
		# necessary number of blocks
    	s = ceil(T/bl)	 
		# blocks starting points
    	matrix c = mrandgen(i, 1, T-bl+1, 1, s)
		# convert to indices of all needed obs	
    	matrix ndx = vec(c .+ seq(0, bl-1)') 

    	## recentring
    	matrix m = mshape(NA, bl, cols(U))
		# calculate respective averages (bl different ones)
    	loop i = 1..bl 
        	m[i,] = meanc(U[i: T-bl+i,])
    	endloop

    	ndx = ndx[1:T]	# cut off "overhanging tail"
    	return U[ndx,] - (ones(s) ** m)[1:T, ]

    else
        funcerr "Shouldn't happen"
    endif

end function

function bundle get_settings (const string key[null])
    /* Function to get a bundle containing libset settings
       (either all of them or one specific one). Added 2024-03-07.
       Correct handling of floating point values depends on git
       master as of 2024-03-06.
    */
    scalar lookup = exists(key)
    bundle ret
    string buf
    outfile --buffer=buf --quiet
        set
    end outfile
    string line setvar setval
    scalar gotkey = 0
    scalar v
    loop while getline(buf, &line) && !gotkey
        if sscanf(line, "%s = %s", setvar, setval) == 2
            if setvar == "workdir"
                setval = sprintf("%s", $workdir)
            endif
            if lookup && setvar == key
                v = atof(setval)
                ret[setvar] = ok(v) ? v : setval
                gotkey = 1
            elif !lookup
                v = atof(setval)
                ret[setvar] = ok(v) ? v : setval
            endif
        endif
    endloop
    if lookup
        # clean-up call
        getline(buf, null)
        # and error check
        if !gotkey
            printf "'%s': key was not found\n", key
        endif
    endif
    return ret
end function
