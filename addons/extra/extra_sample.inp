# extra_sample.inp
# Sample script for the extra.gfn package

include extra.gfn --force
set verbose off

######## Examples for functions not needing a dataset ##########

print "---- nearPSD -----"
matrix mc = {1, -1.2; -1.2, 1}	# eigenvalues -0.2 and 2.2
eval nearPSD(&mc)
print mc

print "---- zeroifclose -----"
mc = {1, 1e-12}
eval zeroifclose(&mc, 1e-10)
print mc

print "---- truncnorm -----"
m = 0.5
s = 1
matrix left = truncnorm(20, m, s, NA, 1)
matrix right = truncnorm(20, m, s, -1, NA)
matrix both = truncnorm(20, m, s, -1, 1)
print left right both

print "---- commute -----" ##
# vec application
eval commute( vec( I(2) ~ ones(2,1) ), 2, 3)
# commutation of the Kronecker product
matrix A = mnormal(4,1)
matrix B = mnormal(1,5)
eval B**A
matrix temp = commute(A**B, rows(B), rows(A))
eval commute(temp, cols(B), cols(A), 1) # post-multipl.; should give B**A

print "---- eliminate -----"
eval eliminate(vec(I(3)))

print "---- duplicate -----"
eval duplicate(vech(I(3)))

print "---- combinations -----"  ##
matrix bucket = {1, 3, 5, 6, 7}
eval combinations(bucket, 3)

print "---- scores2x2 -----"##
eval scores2x2({24, 4; 12, 9})

print "---- WSRpvalue -----"
print "============================="
print "Some examples for WSRpvalue()"
loop N=11..14 -q            # loop over n
  loop i = 6..7 -q        # loop over W
    printf "n=$N, W=$i\n"
    printf "try exact: %g, approx: %g\n", WSRpvalue(N, i), WSRpvalue(N, i, 1)
  endloop

  Wmax = N * (N + 1) / 2
  loop i = (Wmax - 7)..(Wmax - 6) -q
    printf "n=$N, W=$i\n"
    printf "try exact: %g, approx: %g\n", WSRpvalue(N, i), WSRpvalue(N, i, 1)
  endloop

endloop

print "---- WSRcritical -----"
print "----------------------------------------"
print "Some examples for WSRcritical()"
loop N=11..13 -q            # loop over n
  Wmax = N * (N + 1) / 2
  loop i=3..4 -q        # loop over W
    printf "n = $N, Pr = %g\n", i/100
    printf "try exact:"
    eval WSRcritical(N, i/100)'
    printf "approx:"
    eval WSRcritical(N, i/100, 1)'
  endloop
endloop

print "---- powerset -----"
strings pp =  powerset(defarray("a", "b", "c", "x", "y", "z"))
loop i=1..nelem(pp) -q
  eval pp[i]
endloop

print "---- onemode -----"
eval onemode({1,2,3,1,1,2})

print "---- drill -----"
matrix A = mshape(seq(1,9),3,3)
matrix B = A
matrices x = defarray(A, B)
eval drill(x, 2)			# one row
eval drill(x, , 1)			# one column
eval drill(x, 2, 1)			# one row & one column
eval drill(x, {1,3}, 2)		# multiple rows
eval drill(x, 2, {1,3})		# multiple columns

print "---- splitfname -----"  ##
eval splitfname(sprintf("\\do\\.you\\believe.input"))

print "---- multi_instrings -----"  ##
strings AA = defarray("an","apple","and","an","egg")
strings BB = defarray("an","dog","egg")
eval multi_instrings(AA, BB)

######## Examples that require a dataset ############

open denmark	# get some data

print "---- gap_filler -----"  #
series play = LRM
play[10] = NA	# insert missing
series playout = gap_filler(play, 2)
print playout

print "---- winsor -----"  #
series winDM = winsor(ldiff(LRM))
series winDM_asy = winsor(ldiff(LRM), 0.01, 0.8)
print winDM_asy

print "---- correspondence -----"  ##

x = $obsminor == 4
C = seasonals(1,1)

printf "correspondence(x, S4) = %d\n\n", correspondence(x, S4)

print "---- fracorder ------"
open djclose.gdt --quiet	# need longer data here
matrix M = fracorder(ldiff(djclose), 20)
print M

print "---- mat2list ------"

nulldata 20
matrix M = mnormal($nobs, 2)
list X = mat2list(M)
matrix M = muniform($nobs, 3)
cnameset(M, defarray("foo", "bar", "baz"))
list X = mat2list(M)

print --byobs
