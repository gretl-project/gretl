set verbose off
include regls.gfn

# An artificial example with more regressors than observations,
# where the actually relevant regressors are known. With an s
# value (lambda/lambda-max) of around 0.3, ADMM is pretty good
# at picking out the relevant ones. Note that no seed is set, so
# results will differ somewhat from run to run.

n = 80  # observations
k = 120 # regressors
matrix X = mnormal(n,k)
u = mnormal(n, 1)
matrix sel = {5, 20, 37, 60, 70}
matrix y = sumr(X[,sel]) + u
printf "Truly non-zero coefficients:\n"
eval sel

nulldata n --preserve
series ys = y
list LX = null
loop i=1..k -q
  LX += genseries(sprintf("x%d", i), X[,i])
endloop

bundle parms = _(lfrac=0.35, stdize=0)
bundle b = regls(ys, LX, parms)
eval b.nzb
